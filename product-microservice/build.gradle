plugins {
    id 'io.vertx.vertx-plugin' version '1.1.1'
    id 'application'
}

vertx {
    mainVerticle = 'com.ilegra.holiexpress.product.ProductVerticle'
    config = "${projectDir}/conf/config.json"
}

dependencies {
    compile project(':holi-express-common')
    compile group: 'org.postgresql', name: 'postgresql', version: '42.1.4'
    compile group: 'io.vertx', name: 'vertx-core', version: '3.9.1'
    compile group: 'io.vertx', name: 'vertx-web', version: '3.9.1'
    compile group: 'io.vertx', name: 'vertx-config', version: '3.9.1'
    compile group: 'io.vertx', name: 'vertx-jdbc-client', version: '3.9.1'
    compile group: 'io.vertx', name: 'vertx-service-discovery', version: '3.9.1'
    compile group: 'io.vertx', name: 'vertx-codegen', version: '3.9.1'
    compile group: 'io.vertx', name: 'vertx-lang-js', version: '3.9.1'
    compile group: 'io.vertx', name: 'vertx-service-discovery-backend-redis', version: '3.9.1'
    compile group: 'io.vertx', name: 'vertx-rx-java2', version: '3.9.1'
    annotationProcessor group: 'io.vertx', name: 'vertx-codegen', version: '3.9.1'
    testCompile group: 'io.vertx', name: 'vertx-web-client', version: '3.9.1'
    testCompile group: 'io.vertx', name: 'vertx-junit5', version: '3.9.1'
    testCompile group: 'io.vertx', name: 'vertx-unit', version: '3.9.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.9'
    testCompile group: 'org.powermock', name: 'powermock-core', version: '1.7.3'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '1.7.3'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.3'
}

sourceSets {
    generated {
        java.srcDir "${projectDir}/src/generated/java"
    }
}

task generateProxies(type: JavaCompile) {
    group 'build'
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly + configurations.annotationProcessor
    options.annotationProcessorPath = configurations.annotationProcessor
    destinationDir = file("${projectDir}/src/generated/java")
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-Acodegen.output=$destinationDir"
    ]
}

//we are compiling all together on compileJava task
compileGeneratedJava.enabled = false

compileJava {
    dependsOn generateProxies
    source += sourceSets.generated.java
    options.compilerArgs += '-proc:none'
}

clean {
    delete += sourceSets.generated.java.srcDirs
}

jar {
    manifest {
        attributes "Main-Class": "io.vertx.core.Launcher"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
